attribute vec3 vertexPosition;
attribute vec3 vertexNormal;
attribute vec2 vertexTexCoord;
attribute float firstBoneIndex;
attribute float secondBoneIndex;
attribute float thirdBoneIndex;
attribute float fourthBoneIndex;
attribute float firstWeight;
attribute float secondWeight;
attribute float thirdWeight;
attribute float fourthWeight;

uniform mat4 worldMatrix;
uniform mat4 viewProjMatrix;
uniform mat4 boneMatrices[30];

varying vec3 fragmentWorldSpacePosition;
varying vec3 fragmentNormal;
/*varying vec3 fragmentTangent;
varying vec3 fragmentBitangent;*/
varying vec2 texCoord;

vec3 transformVectorByBone(int boneIndex, vec3 v);
vec3 transformVectorByBoneRotationOnly(int boneIndex, vec3 v);
mat4 getBoneMatrix(int boneIndex);

void main(void) 
{
	// Calculate the skinned position.
	vec3 positionTransformedByFirstBone = transformVectorByBone(int(firstBoneIndex), vertexPosition);
	vec3 positionTransformedBySecondBone = transformVectorByBone(int(secondBoneIndex), vertexPosition);
	vec3 positionTransformedByThirdBone = transformVectorByBone(int(thirdBoneIndex), vertexPosition);
	vec3 positionTransformedByFourthBone = transformVectorByBone(int(fourthBoneIndex), vertexPosition);

	vec3 positionTransformedByBones = 
		(positionTransformedByFirstBone * firstWeight) +
		(positionTransformedBySecondBone * secondWeight) +
		(positionTransformedByThirdBone * thirdWeight) +
		(positionTransformedByFourthBone * fourthWeight);
	
	vec4 tempWorldSpacePosition = worldMatrix * vec4(positionTransformedByBones, 1.0);

	// Calculate the skinned normal.
	vec3 normalTransformedByFirstBone = transformVectorByBoneRotationOnly(int(firstBoneIndex), vertexNormal);
	vec3 normalTransformedBySecondBone = transformVectorByBoneRotationOnly(int(secondBoneIndex), vertexNormal);
	vec3 normalTransformedByThirdBone = transformVectorByBoneRotationOnly(int(thirdBoneIndex), vertexNormal);
	vec3 normalTransformedByFourthBone = transformVectorByBoneRotationOnly(int(fourthBoneIndex), vertexNormal);

	vec3 normalTransformedByBones = 
		(normalTransformedByFirstBone * firstWeight) +
		(normalTransformedBySecondBone * secondWeight) +
		(normalTransformedByThirdBone * thirdWeight) +
		(normalTransformedByFourthBone * fourthWeight);
	
	vec3 tempWorldSpaceNormal = normalize(mat3(worldMatrix) * normalTransformedByBones);
	//vec3 tempWorldSpaceNormal = vertexNormal;

	// Output values.
	fragmentWorldSpacePosition = tempWorldSpacePosition.xyz;

	texCoord = vertexTexCoord;

	fragmentNormal = tempWorldSpaceNormal;

    gl_Position = viewProjMatrix * tempWorldSpacePosition;
}

vec3 transformVectorByBone(int boneIndex, vec3 v) 
{
	if (boneIndex != -1) 
	{
		mat4 boneMatrix = getBoneMatrix(boneIndex);

		vec4 vectorTransformedByBone = boneMatrix * vec4(v, 1.0);

		return vectorTransformedByBone.xyz;
	}
	else 
	{
		return vec3(0, 0, 0);
	}
}

vec3 transformVectorByBoneRotationOnly(int boneIndex, vec3 v) 
{
	if (boneIndex != -1) 
	{
		mat4 boneMatrix = getBoneMatrix(boneIndex);

		vec3 vectorTransformedByBone = mat3(boneMatrix) * v;

		return vectorTransformedByBone;
	}
	else 
	{
		return vec3(0, 0, 0);
	}
}

mat4 getBoneMatrix(int boneIndex) 
{
	for (int i = 0; i < 30; i++) 
	{
        if (i == boneIndex) 
		{
			return boneMatrices[i];
		}
    }

	return boneMatrices[0]; // This just shuts the compiler up. This should never happen.
}